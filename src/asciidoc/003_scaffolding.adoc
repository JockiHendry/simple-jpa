== Scaffolding

=== Usage

Scaffolding feature from simple-jpa can be used to generate MVC groups based on existing domain classes.  This is very
useful in the early phase of application development as it provides guide for further development.
Scaffolding can be invoked by using <<generate-all>> command or by enabling it in
_Config.groovy_ (see <<scaffolding-2>>).

For example, MVC groups for domain class `Student` can be generated by using the following command:

....
griffon generate-all Student
....

simple-jpa will searching for `Student` class in `griffon-app/conf/metainf/persistence.xml`.  There is nothing to worry
here because all domain classes created by <<create-domain-class>> will automatically listed in `persistence.xml`.

Based on `Student` class, simple-jpa will perform the following actions:

* Create a new `griffon-app/models/project/StudentModel.groovy` if it doesn't exists.
* Create a new `griffon-app/views/project/StudentView.groovy` if it doesn't exists.
* Create a new `griffon-app/controllers/project/StudentController.groovy` if it doesn't exists.
* Search for `student` MVC group.  If it doesn't exists, create a new MVC group with the following content:
+
[source,groovy]
.Application.groovy
----
// MVC Group for "student"
'student' {
    model      = 'project.StudentModel'
    view       = 'project.StudentView'
    controller = 'project.StudentController'
}
----
* Create a new `test/integration/project/StudentTest.groovy` if it doesn't exists.
* Create a new `data.xls` if it doesn't exists.  Add a single empty sheet named as `student` to the Microsoft Excel
file.  This Excel file will act as data source when invoking `StudentTest.groovy`.  See <<testing>> for more
information.
* Add messages to `griffon-app/i18n/messages.properties` if they don't exists.
* Add a generic exception handler in `griffon-app/conf/Events.groovy` if it doesn't exists.

By default, simple-jpa will not modify the content of existing files.  To force simple-jpa to overwrite existing
files, add `-forceOverwrite` parameter when invoking `generate-all`.  For example:

....
griffon generate-all -forceOverwrite Student
....

The default target package for generated MVC artifacts is `project`.  To use a different target package,
`-generatedPackage` should be passed when invoking `generate-all`.  For example:

....
griffon generate-all -generatedPackage=com.jocki.exercises Student
....

The command will place generated MVC artifacts in `com.jocki.exercises` package and register the following MVC group:

[source,groovy]
.Application.groovy
----
// MVC Group for "student"
'student' {
    model      = 'com.jocki.exercises.StudentModel'
    view       = 'com.jocki.exercises.StudentView'
    controller = 'com.jocki.exercises.StudentController'
}
----

simple-jpa is smart enough to recognize package hierarchy in domain classes.  It will try to preserve the
source package hierarchy in the resulting MVC artifacts.  For example, if `Teacher` is located in `domain.staff`,
then the following command:

....
griffon generate-all -generatedPackage=com.jocki.exercises Teacher
....

will place the generated MVC artifacts in `com.jocki.exercises.staff` and register the following MVC group:

[source,groovy]
----
// MVC Group for "teacher"
'teacher' {
    model      = 'com.jocki.exercises.staff.TeacherModel'
    view       = 'com.jocki.exercises.staff.TeacherView'
    controller = 'com.jocki.exercises.staff.TeacherController'
}
----

In Griffon, startup group is the first MVC group to be instantiated and displayed.  By default, `generate-all` will
not modify the value of current startup group.  This behaviour can be changed by using `-setStartup` parameter.  If
`-setStartup` is passed to `generate-all`, the command will set startup group to the generated MVC group.  For
example, the following command will generate `teacher` MVC group and set it as startup group:

....
griffon generate-all -setStartup Teacher
....

[IMPORTANT]
====
Since 0.8, simple-jpa generates view as a panel.  Because the generated view doesn't have top level container
such as `JFrame`, it can't be displayed by itself.  The recommended way to present the view is by creating a
startup group that will act as top level container.
====

simple-jpa can also create a dedicated startup group that serves as container for existing domain class
based MVC groups.  To generate a dedicated startup group, `-startupGroup` parameter should be used with
the startup group's name as its value.  For example, the following command will generate `mainGroup` MVC group and set
 it as startup group:

....
griffon generate-all -startupGroup=MainGroup
....

The generated startup group's view has a toolbar to launch domain class based MVC groups as shown in the following
result:

image::default_startup_group.PNG[]

Domain class based MVC group generation and startup group generation can also be combined in a single command execution.
For example:

....
griffon generate-all Teacher -startupGroup=MainGroup
....

[TIP]
====
New MVC groups may be generated in the future after startup group has been created.  These new MVC groups will not
appear in current startup group's toolbar.  One of possible solutions is recreate the startup group by including
`-forceOverwrite` to overwrite existing startup group:

....
griffon generate-all -startupGroup=MainGroup -forceOverwrite
....
====

`generate-all` can accept multiple domain classes separated by space.  For example:

....
griffon generate-all Student Teacher Classroom
....

As a shortcut, `*` can be used as domain class name to generate all registered domain classes. For example:

....
griffon generate-all *
....

Since version 0.8, scaffolding in simple-jpa is performed by an instance of `Generator`.  The default generator
that shipped with simple-jpa is `simplejpa.scaffolding.generator.basic.BasicGenerator`.  To select another generator,
 add `-generator` parameter when invoking `generate-all`.  For example:

....
griffon generate-all -generator=my.custom.Generator *
....

The parameters that have been discussed so far can also be stored in `Config.groovy`.  See <<scaffolding-2>> for
list of all available configuration keys. For example, running `griffon generate-all *` with the following
configurations:

[source,groovy]
.Config.groovy
----
griffon {
    simplejpa {
        scaffolding {
            generatedPackage = 'com.jocki.exercises'
            startupGroup = 'MainGroup'
            forceOverwrite = true
        }
    }
}
----

is identical with executing the following command:

....
griffon generate-all -generatedPackage=com.jocki.exercises -startupGroup=MainGroup -forceOverwrite *
....

Another benefit of storing `generate-all` parameters as configuration keys is automatic scaffolding.  The scaffolding
process can be automated if `griffon.simplejpa.scaffolding.auto` is set to `true`.  For example, this configuration will
automatically run scaffolding process:

[source,groovy]
----
griffon {
    simplejpa {
        scaffolding {
            auto = true
            startupGroup = 'MainGroup'
        }
    }
}
----

Automatic scaffolding will be performed whenever Griffon is compiling classes, such as when project is launched from
`griffon run-app` command.  Automatic scaffolding will not run in production when no class compilations being carried
out.

=== Basic Generator

Basic generator is the default generator used by simple-jpa.  It supports the following attribute types in
domain class:

.Supported Attribute Types
|===
|Attribute Type | SwingBuilder node | Class

|String, Character
|textField()
|JTextField

|Boolean
|checkBox()
|JCheckbox

|Byte, Short, Integer, Long, Float, Double, BigInteger
|numberTextField()
|JFormattedTextField

|BigDecimal
|decimalTextField()
|JFormattedTextField

|DateTime, LocalDateTime, LocalDate, LocalTime
|dateTimePicker()
|DateTimePicker

|Enum
|comboBox()
|JComboBox with EnumComboBoxModel

|List, Set
|button() if relation is one-to-many or tagChooser() if relation is many-to-many
|JButton or TagChooser

|Any Entity Object
|button() if relation is one-to-one or comboBox() if relation is one-to-many
|JButton or JComboBox
|===

WARNING: Basic generator doesn't support native data types such as `int`, `float`, or `double` because they
are not nullable.

TIP: Basic generator will generate TODO comments in the generated code to provide information or warning to
 user.  It is safe to delete these TODO comments.

For example, the following domain class:

[source,groovy]
----
@DomainClass @Entity @Canonical
class Student {

    String name

    Integer age

    @Type(type = "org.jadira.usertype.dateandtime.joda.PersistentLocalDate")
    LocalDate birthDate

    Boolean registered

    @Enumerated
    GRADE grade

    @ManyToOne
    Teacher teacher

    @ManyToMany(cascade=CascadeType.ALL, fetch=FetchType.EAGER)
    List<Classroom> classrooms = []

}

enum GRADE {
    GRADE_1, GRADE_2, GRADE_3
}
----

will be generated as:

image::generated_view_basic_attributes.PNG[]

To create a new record, user should enter required values in the editing area and click the _'Save'_ button.

To update existing record, user must first select a row in table, enter the updated values in the editing area, and
click the _'Save'_ button.

To remove record from database, user must click the _'Delete'_ button that will appear if table's row is selected.

For domain class that have one-to-one association, basic generator generates a dialog to
create, edit or remove the related entity.  This feature requires cascading to be activated for the attribute.

For example, the following attribute declaration:

[source,groovy]
----
@OneToOne(cascade=CascadeType.ALL, orphanRemoval=true)
Teacher teacher
----

is represented by `JButton` that if clicked will open a dialog that allows user to modify the related
 `Teacher`.
 If this button is clicked in create operation, the dialog can be used to create a new instance of `Teacher` entity:

image::generated_view_one_to_one.png[]

If this button is clicked in update operation, the dialog can be used to update or delete existing `Teacher` entity:

image::generated_view_one_to_one_update.png[]

The naming convention for one-to-one MVC group and its artifact's name is the target entity name
with _'AsPair'_  as suffix.  For example, if target entity is `Teacher`, basic generator generates
`TeacherAsPairModel`, `TeacherAsPairView`, and `TeacherAsPairController`.  They are not to be confused with the
standalone MVC group for `Teacher` such as `TeacherModel`, `TeacherView`, and `TeacherController` that may also exists
in the project (for example they are used for displaying list of `Teacher` in their own screen and *not* as popup).

IMPORTANT: If the annotated attribute in the generated code doesn't have `cascade` set to `CascadeType.ALL`,
`org.hibernate.TransientPropertyValueException` will be raised when saving the entity.  To fix this, either add a
proper cascading to object mapping or change the generated code to make it work without cascading.

Basic generator also treats `@Embedded` attributes as equivalent of one-to-one attributes.  For example, the
following mapping generates the same view as the previous one:

[source,groovy]
----
@Embedded
Teacher teacher
----

TIP:  User can press ESC button as a shortcut to close popup dialog.

TIP:  User can press Enter or double click a selected row in table to display the first one-to-many or one-to-one
popup dialog in the view.

For one-to-many associations, basic generator also generates a dialog.  For example, the following attribute
declaration:

[source,groovy]
----
@OneToMany(cascade=CascadeType.ALL, orphanRemoval=true, fetch=FetchType.EAGER)
Set<Classroom> classrooms = new HashSet<>()
----

is represented by `JButton` that if clicked will open a dialog that allows user to add or remove the list of
`Classroom` entities that are associated with current entity. If this button is clicked in create operation, the
dialog can be used to populate the collection with one or more `Classroom`:

image::generated_view_one_to_many.png[]

If this button is clicked in update operation, the dialog can be used to add new entity to the collection, edit
the value of entity in the collection, or remove an entity from the collection:

image::generated_view_one_to_many_update.png[]

The naming convention for one-to-many attribute is the target entity name with _'AsChild'_  as suffix.  For example, if
target entity is `Classroom`, basic generator generates `ClassroomAsChildModel`, `ClassroomAsChildView`, and
`ClassroomAsChildController`.  This is not to be confused with `ClassroomModel`, `ClassroomView` or
`ClassroomController` that may also exists in the project.

Basic generator will also treat `@ElementCollection` attributes as equivalent of one-to-many attributes. For
example, the following mapping creates the same view as the previous one:

[source,groovy]
----
@ElementCollection(fetch=FetchType.EAGER)
Set<Classroom> classrooms = new HashSet<>()
----

For bidirectional associations, basic generator generates inverse attributes as labels because they are not editable.
 For example, the following domain classes:

[source,groovy]
----
@DomainClass @Entity @Canonical
class Invoice {

    String number

    @OneToOne(cascade=CascadeType.ALL, orphanRemoval=true)
    Delivery delivery

}

@DomainClass @Entity @Canonical(excludes='invoice')
class Delivery {

    String ticketNumber

    @OneToOne(mappedBy='delivery')
    Invoice invoice

}
----

will be generated as:

image::generated_view_one_to_one_bidirectional.png[]

TIP: Don't forget to add `excludes` to `@Canonical` in bidirectional association to avoid infinitive recursion!

=== DDD Generator

simple-jpa also shipped with a DDD generator that can selected by using `-generator` parameter such as in the following
execution:

....
griffon generate-all -generator=simplejpa.scaffolding.generator.ddd.DDDGenerator *
....

DDD generator rely on basic generator to perform most of its works.  It generates views that are identical to those
generated by basic generator.  The only distinction is this generator will move JPA related methods and database
transactions from controller into a separate repository.

For every `@Entity` annotated domain classes, DDD generator will create their corresponding repository classes.
simple-jpa supports a custom Griffon's artifact called `repository`.  For example, if there is an entity called
`Student`, DDD generator will create a new repository called `StudentRepository` in `griffon-app/repositories`.  This
artifact is a singleton.  It is automatically injected into controller (and other Griffon's artifacts) by defining
a variable such as `studentRepository`.

To retrieve all repositories, use `RepositoryManager.getRepositories()` such as:

[source,groovy]
----
def repositories = SimpleJpaUtil.instance.repositoryManager.repositories
println "All repositories: $repositories"
----

Repository artifact is lazy initialized.  This means it won't be created if it is not used.  `RepositoryManager.findRepository()`
can be used to retrieve an instance of repository.  This method will create a new instance that will be reused later (singleton)
when it is called for the first time.

[source,groovy]
----
InvoiceRepository repository = SimpleJpaUtil.instance.repositoryManager.findRepository('Invoice')
----

DDD generator disables dynamic methods in controller and adds dynamic methods to repository by adding the following line to _Config.groovy_:

[source,groovy]
----
griffon.simplejpa.finders.injectInto = ['repository']
----

=== Customization

A generator usually has classes and multiple templates.  Reusable logic for view generation is stored in
classes.  They will be invoked by templates.  For example, the generator class is always available to template as `g`
variable.

The easiest way to customize a generator is to modify its template. `install-templates` command can be used to
install templates for simple-jpa built-in generator to current project:

....
griffon install-templates
....

The command will copy template files to _src/templates/artifacts_.  The following is list of all template files used by
simple-jpa built-in generators:

.Template Files
|===
|Name | Generator | Purpose

|SimpleJpaDomainClass.groovy
|create-domain-class
|Domain class generation

|SimpleJpaRepository.groovy
|DDD
|Repository for entity

|StartupModel.groovy
|Basic, DDD
|Startup group's model

|StartupController.groovy
|Basic, DDD
|Startup group's controller

|StartupView.groovy
|Basic, DDD
|Startup group's view

|SimpleJpaModel.groovy
|Basic, DDD
|Domain-class based model

|SimpleJpaView.groovy
|Basic, DDD
|Domain-class based view

|SimpleJpaController.groovy
|Basic
|Domain-class based controller

|SimpleJpaDDDController.groovy
|DDD
|Domain-class based controller

|SimpleJpaIntegrationTest.groovy
|Basic, DDD
|Integration test case

|SimpleJpaPairModel.groovy
|Basic, DDD
|one-to-one popup model

|SimpleJpaPairView.groovy
|Basic, DDD
|one-to-one popup view

|SimpleJpaPairController.groovy
|Basic
|one-to-one popup controller

|SimpleJpaDDDPairController.groovy
|DDD
|one-to-one popup controller

|SimpleJpaChildModel.groovy
|Basic, DDD
|one-to-many popup model

|SimpleJpaChildView.groovy
|Basic, DDD
|one-to-many popup view

|SimpleJpaChildController.groovy
|Basic
|one-to-many popup controller

|SimpleJpaDDDChildController.groovy
|DDD
|one-to-many popup controller
|===

After these templates have been installed into current project, the next invocation of `generate-all` will be based on
them.

For a more complex customization, a new generator may be created.  The new generator can be extended from existing
generator or `simplejpa.scaffolding.generator.Generator`. All generators have the following important methods:

* `generate(DomainClass domainClass)` will be invoked when generating files for individual domain class.
* `generateStartupGroup(Map<String,DomainClass> domainClasses)` will be invoked when generating startup group.
* `generateExtra(Map<String,DomainClass> domainClasses)` is an optional method that will be invoked after generating
files for individual domain class.

For example, the following is a sample declaration of custom generator that does nothing:

[source,groovy]
----
package generator

import simplejpa.scaffolding.DomainClass
import simplejpa.scaffolding.Scaffolding
import simplejpa.scaffolding.generator.basic.BasicGenerator

class MyGenerator extends BasicGenerator {

    MyGenerator(Scaffolding scaffolding) {
        super(scaffolding)
    }

    @Override
    void generate(DomainClass domainClass) {
        println "Generating ${domainClass.name}"
        // call super.generate(domainClass) for default operation (generating MVC artifacts)
    }

    @Override
    void generateStartupGroup(Map<String, DomainClass> domainClasses) {
        println "Generating startup group..."
    }

    @Override
    void generateExtra(Map<String, DomainClass> domainClasses) {
        println "Generating extra..."
    }

}
----

The following command will use the custom generator:

....
griffon -generator=generator.MyGenerator *
....

